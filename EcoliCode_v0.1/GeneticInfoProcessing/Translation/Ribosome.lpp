// BSD 3-Clause License
// Â© 2022, The University of Texas Southwestern Medical Center. All rights reserved.
// Donghoon M. Lee and Daehwan Kim

// Ribosomes

RNA tRNA;
complex AA_tRNA; // Charged tRNA

protein IF; // Initiation Factor
protein EF_Tu, EF_G; // Elongation Factors
protein RF, RRF; // Termination (release) Factors

// initiation
protein R30S
{
    // translation initiation:
    reaction InitialtRNAloading(self + IF + tRNA + mRNA.startcodon --> self~mRNA.startcodon~tRNA~IF, Kd=1e-9);
}

protein R50S;

complex R70S
{
    reaction Complexation(R30S~mRNA.startcodon~tRNA~IF + R50S --> self~tRNA + IF, Kd=1e-9); // self = R30S~R50S~mRNA

    // translation elongation:
    reaction CodonRecognition(self~tRNA + EF_Tu~AA_tRNA --> self~AA_tRNA + EF_Tu + tRNA, Kd=1e-9); // AA_tRNA Loading and tRNA Release
    reaction Elongation
    {
        float R = 20.0;
        reaction PeptidylTransfer(self~tRNA~AA_tRNA~(peptide_{n}) + EF_G --> self~tRNA~tRNA~(peptide_{n+1})~EF_G, r=R); // Assume 10 ATP used for the whole process: 4 ATP for building block synthesis, 5.5 ATP for the rest of the process in rich media
        reaction RNATranslocation(self.mRNA.N{n}~tRNA~tRNA~peptide~EF_G --> self.mRNA.N{n+3}~tRNA~tRNA~peptide + EF_G, r=R);
    }

    // translation termination:
    reaction StopCodonRecognition(self.mRNA.stopcodon~tRNA~tRNA~peptide + RF --> self~tRNA~peptide~RF + tRNA, Kd=1e-9);
    reaction PeptideRelease(self~tRNA~peptide~RF + RRF --> self~tRNA~RRF + RF + peptide, Kd=1e-9);
    reaction Dissociation(self~tRNA~RRF --> R30S + R50S + mRNA + tRNA + RRF, Kd=1e-9);
}

protein AA_tRNASynthetase
{
    reaction tRNACharging(tRNA + AA --> AA_tRNA, KM=1e-9);
}
