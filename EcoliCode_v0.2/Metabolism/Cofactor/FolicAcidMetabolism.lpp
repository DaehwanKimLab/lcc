// BSD 3-Clause License
// Â© 2023, The University of Texas Southwestern Medical Center. All rights reserved.
// Donghoon M. Lee and Daehwan Kim

// PurineMetabolism

from MetaboliteConcentrations import *;

import MetaboliteConcentrations;
import EcoliInfo;

Init_DHF = MetaboliteConcentrations.DHF;

pathway FolicAcidMetabolism
{

    reaction DHFSynthesis( -> DHF)
    specification
    {
        float rate = EcoliInfo.DNAReplicationRate * EcoliInfo.C2M;

        if (DHF >= Init_DHF)
        {
            DHF' = 0;
        }
        else
        {
            if (DHF + rate >= Init_DHF)
            {
                rate = Init_DHF - DHF;
            }
            DHF' = rate;
            // DHF' = min(rate, Init_DHF - DHF)
        }
    }

    reaction THFSynthesisByFolA(DHF -> THF)
    specification
    {
        float rate = EcoliInfo.DNAReplicationRate * EcoliInfo.C2M * 2;
        float max_rate = DHF + THF / 2;

        if (rate > max_rate)
        {
            THF' = max_rate;
        }
        else
        {
            THF' = rate;
        }
    }
    
    reaction FiveTenMethyleneTHFSynthesisByGlyA(THF -> M_5_10_methylene_THF)
    specification
    {
        float rate = EcoliInfo.DNAReplicationRate * EcoliInfo.C2M * 2;
        float max_rate = THF + M_5_10_methylene_THF / 2;

        if (rate > max_rate)
        {
            M_5_10_methylene_THF' = max_rate;
        }
        else
        {
            M_5_10_methylene_THF' = rate;
        }
    }
    
    reaction TenFormylTHFSynthesis(M_5_10_methylene_THF -> M_10_formyl_THF)
    specification
    {
        float rate = EcoliInfo.DNAReplicationRate * EcoliInfo.C2M;
        float max_rate = M_5_10_methylene_THF + M_10_formyl_THF / 2;

        if (rate > max_rate)
        {
            M_10_formyl_THF' = max_rate;
        }
        else
        {
            M_10_formyl_THF' = rate;
        }
    }
}
