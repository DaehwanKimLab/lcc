// Donghoon M. Lee, Austin Marckx, and Daehwan Kim
// Ribosomes

import tRNA;

import InitiationFactor;
import ElongationFactor;
import TerminationFactor;

tRNA tRNA_Init = tRNA.tRNA(Met)_Init;

protein IF1 = InitiationFactor.IF1;
protein IF2 = InitiationFactor.IF2;
protein IF3 = InitiationFactor.IF3;

protein EF-Tu = ElongationFactor.EF-Tu;
protein EF-Ts = ElongationFactor.EF-Ts;
protein EF-G = ElongationFactor.EF-G;

protein RF1 = TerminationFactor.RF1;
protein RF2 = TerminationFactor.RF2;
protein RF3 = TerminationFactor.RF3;
protein RRF = TerminationFactor.RRF;
protein RFstopcodon { RF1, RF2 }

// initiation
protein 30S // update to complex (23S, 5S, rRNAs: ~34 proteins)
{
    domain ASite;
    domain PSite;
    domain ESite;
}
protein 50S;
// protein 50S // update to complex (16S, rRNA: ~21 proteins)
// {
//     domain ASite; // aminoacyl (or acceptor) site
//     domain PSite; // peptidyl site
//     domain ESite; // exit site
// }

complex 30S_PIC
{
    reaction 30S_PICAssembly(30S + IF1 + IF2~GTP + IF3, Kd=1e-9); // 30S Preinitiation Complex
    reaction InitiatortRNAloading(self + tRNA_Init --> self.PSite~tRNA_Init, Kd=1e-9); // initiator tRNA loading
}

complex 30S_IC
{
    self = 30S_PIC.PSite~tRNA_Init + 30S_PIC~mRNA.startcodon;
    reaction 30S_ICAssembly(30S_PIC.PSite~tRNA_Init + mRNA.startcodon --> self, Kd=1e-9); // 30S Initiation Complex, codon recognition reaction
}

complex 70S_PIC(30S_IC + 50S, Kd=1e-9); // 70S Preinitiation Complex

complex 70S // 70S Initiation Complex
{
    self        = 30S~50S;
    70S_tRNA    = self.ESite        + self.PSite~tRNA~peptide   + self.ASite~AA_tRNA        + self.30S~mRNA; // tRNA loaded
    70S_Pre     = self.ESite        + self.PSite~tRNA           + self.ASite~tRNA~peptide   + self.30S~mRNA; // pretranslocation
    70S_Post    = self.ESite~tRNA   + self.PSite~tRNA~peptide   + self.ASite                + self.30S~mRNA; // posttranslocation
    70S_Term    = self.ESite        + self.PSite~tRNA           + self.ASite                + self.30S~mRNA;

    reaction 70S_ICAssembly(70SPIC + H2O --> self.PSite~tRNA_Init~mRNA.startcodon + IF1 + IF2~GDP + Pi + IF3, Kd=1e-9);

    // elongation:
    reaction CodonRecognition(70S_Post + 70S_Post.30S~mRNA.NNN{n} + EF-Tu~GTP~AA_tRNA --> 70S_tRNA~EF-Tu~GTP + tRNA, Kd=1e-9); // tRNALoadingAndRelease
    reaction GTPHydrolysis(70S_tRNA~EF-Tu~GTP + H2O --> 70S_tRNA + EF-Tu~GDP + Pi, Kd=1e-9);
    reaction PeptidylTransfer(self.PSite~tRNA~peptide_{n} + self.ASite~tRNA~AA_tRNA --> self.PSite~tRNA + self.ASite~tRNA~peptide_{n+1}, r=20.0);
    reaction EF-G_Binding(70S_Pre + EF-G~GTP --> 70S_Pre.50S~EF-G~GTP, Kd=1e-9);
    reaction RNATranslocation
    {
        reaction EF-G_GTPHydrolysis(70S_Pre.50S~EF-G~GTP + H2O --> 70S_Post.50S~EF-G~GDP + Pi, Kd=1e-9);
        reaction EF-G_Release(70S_Post.50S~EF-G~GDP --> 70S_Post + EF-G~GDP, Kd=1e-9);
    }
    
    // termination:
    reaction StopCodonRecognition(70S_Post.ESite~tRNA + 70S_Post.30S~mRNA.stopcodon + RFstopcodon --> 70S_Post.ASite~RFstopcodon + tRNA, Kd=1e-9); // RF1 or RF2 binding
    reaction PeptideRelease(70S_Post.ASite~RFstopcodon + RF3~GDP --> 70S_Term.ASite~RFstopcodon + 70S_Term~RF3~GDP + peptide, Kd=1e-9); // RF3 binding
    reaction RF12Release(70S_Term.ASite~RFstopcodon + 70S_Term~RF3~GDP + GTP --> 70S_Term~RF3~GTP + GDP + RFstopcodon, Kd=1e-9); 
    reaction RF3Release(70S_Term~RF3~GTP + H2O --> 70S_Term + RF3~GDP + Pi, Kd=1e-9);
    reaction RRF_EF-G_Binding(70S_Term + RRF + EF-G~GTP --> 70S_Term.ASite~RRF + 70S_Term.50S~EF-G~GTP, Kd=1e-9);
    reaction 70SDissociation(70S_Term.ASite~RRF + 70S_Term.50S~EF-G~GTP + H2O --> 30S + 50S + tRNA + RRF + EF-G~GDP + Pi + mRNA, Kd=1e-9);
}
