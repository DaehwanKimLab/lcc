// Donghoon M. Lee, Austin Marckx, and Daehwan Kim
// Bacterial Chemotaxis

// CheA reference
A   = CheA.LBD          + CheA.RD;
AL  = CheA.LBD~Ligand   + CheA.RD;
Am  = CheA.LBD          + CheA.RD~CH3;
AmL = CheA.LBD~Ligand   + CheA.RD~CH3;

protein CheA
{
    domain LBD, RD; // ligand binding domain, regulatory domain

    reaction A_Am_LigandBinding(self.LBD + Ligand{out} --> self.LBD~Ligand, k=1e9, krev=1); // takes care of both A and Am, reversible
    reaction B_Phosphorylation(Am + CheB{in} + ATP{in} --> Am + CheB~Pi{in} + ADP{in} + H{in}, k=0.05e9, krev=0.005) // reversible
} 

protein CheR(CheA.RD + CH3 --> CheA.RD~CH3, kcat=1, KM=1e-10nM); // A_AL_Methylation; takes care of both A and AL

protein CheB
{
    reaction AmL_Demethylation(self~Pi + AmL --> self~Pi + AL + CH3, kcat=1, KM=1nM);
    reaction Am_Demethylation(self~Pi + Am --> self~Pi + A + CH3, kcat=200, KM=1nM);
}

pathway BacterialChemotaxis 
{
    membrane CellMembrane
    {
        CheA = 500nM;

        cytosol Cytosol
        {
            CheR = 5nM, CheB = 0.1nM;
        }
    }
}

// more complete model found below
reference BitbolWingreen_2015
{
    title = "Fundamental Constraints on the Abundances of Chemotaxis Proteins.";
    doi = "https://doi.org/10.1016/j.bpj.2015.01.024";
}
// CheA = 2.97 uM, CheY = 9.73 uM, CheZ = 3.80 uM, CheB = 0.28 uM, CheR = 0.17 uM, FliM = 1.43 uM, where 16% of FliM that are free are discounted.


// independent simulation
if (__name__ == "__main__") // python style
{

    lab L // default dimension (i.e. 1000 by 1000 by 0)
    {
        petridish P
        {
            specification P_S(shape = round, radius = 150mm);

            ecoli E(400, 500, 0)
            {
                pathway BacterialChemotaxis;
            }
            E(500, 600, 0) = 1;
            glucose Ligand(800, 500, 0) = 100nmol;
        }
        P(200, 400, 0) = 1;
    }
    L = 1;

}
